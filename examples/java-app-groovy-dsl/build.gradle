plugins {
    id("java")
    alias(libs.plugins.oci)
}

group = "org.example"
version = "1.0.0"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

tasks.named("jar") {
    manifest.attributes["Main-Class"] = "org.example.java.app.Main"
}

repositories {
    mavenCentral()
}

oci {
    registries {
        dockerHub {
            optionalCredentials()
        }
    }
    imageDefinitions.register("main") {
        allPlatforms {
            dependencies {
                runtime.add("library:eclipse-temurin:21.0.2_13-jre-jammy")
            }
            config {
                entryPoint = ["java", "-jar"]
                entryPoint.add(tasks.named("jar").flatMap { it.archiveFileName })
            }
            layer("main") {
                contents {
                    from(tasks.named("jar"))
                }
            }
        }
    }
}

testing {
    suites {
        test {
            useJUnitJupiter()
            dependencies {
                implementation(libs.testcontainers)
                implementation(libs.gradleOci.junitJupiter)
            }
            oci.of(it) {
                imageDependencies {
                    runtime.add(project).tag("latest")
                }
            }
        }
    }
}
